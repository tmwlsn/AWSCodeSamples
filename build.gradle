plugins {
    id 'application'
}

group 'com.canmage.awscodesamples'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation platform('software.amazon.awssdk:bom:2.17.239')
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.1'

    // lombok
    implementation 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'

    // obfuscation
    implementation 'com.yworks:yguard:3.1.0'
    implementation 'com.yworks:annotation:3.1.0'
}

test {
    useJUnitPlatform()
}

def obfuscate = tasks.register('obfuscate') {
    dependsOn jar
    group 'yGuard'
    description 'Obfuscates and shrinks the java archive.'

    doLast {
        ant.taskdef(
                name: 'yguard',
                classname: 'com.yworks.yguard.YGuardTask',
                classpath: sourceSets.main.runtimeClasspath.asPath
        )

        def archivePath = jar.archiveFile.get().asFile.path
        ant.yguard {
            inoutpair(in: archivePath, out: archivePath.replace(".jar", ".jar"))
            rename(logfile: "${buildDir}/yguard.log.xml") {
                property(name: "error-checking", value: "pedantic")
            }
        }
    }
}

task buildZip(type: Zip) {
    dependsOn obfuscate
    from processResources
    into('lib') {
        from jar
        from configurations.runtimeClasspath
    }
}